#!/usr/bin/env yell

code_start;

/* This is the main test suite of Yell; */

/* intro */
    println 'Hi! This is a sample test of Yell.';
    println '';

/* variables test */
    var 'var1': 'Variables are';
    var 'var2': 'working as expected.';
    println '{var1} {var2}';
    println '';

/* if and bool test */
    if b`n'6' > n'5'`: println 'Booleans and if statements seem to be working as expected.';
    println '';

/* repeat test */
    repeat n'5': println 'If this line was repeated 5 times, repeat statements are working as expected.';
    println '';

/* while test */
    var 'counter': n'1';
    while b`n'{counter}' <= n'2'`: println 'If this line was repeated 2 times, while statements are working as expected.'
      && var 'counter': n'{counter}+1';
    println '';

/* alias test */
    alias 'alias_test': println 'Aliases seem to be working as expected.';
    a 'alias_test';
    println '';

/* python test */
    python 'print(\'Hello from Python! If you see this line, running Python code from Yell is working as expected.\')';
    println '';

/* expressions test */
    print '5 + 5 = ';
    println n'5 + 5';
    println 'If the above line said \'5 + 5 = 10\', expressions (n strings) are working as expected.';
    println '';

/* global libs test */
    import '<sample_lib.yell>';
    a 'sample_lib';
    println '';

/* local imports test */
    import 'imports/import_test1.yell';
    println '';

/* read test */
    read 'You can type anything here: ' 'sample_input';
    print 'If you find what you wrote in this line, reading input (read) is working as expected: ';
    println '{sample_input}';
    println '';

/* expr test */
    read 'You can type any expression here: ' 'sample_input';
    print 'If you find the solution to the expression in this line, solving of expressions in variables is working as expected: ';
    println n'{sample_input}';
    println '';

/* sleep test */
    println 'Sleeping for 2 seconds.';
    sleep n'2';
    println 'If the program halted for 2 seconds, sleep\'s working as expected.';
    println '';

/* comments test */
    println 'If there was no error just now, comments (/* */) are working as expected.';
    println '';

/* externs test */
    system 'echo \'Running OS commands (system) is working as expected.\'';
    println '';

/* end, and another read test ;) */
    read 'Press ENTER to exit: ' 'enter_input';